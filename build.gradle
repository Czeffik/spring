buildscript {
    ext {
        repos = {
            gradlePluginPortal()
            mavenLocal()
            mavenCentral()
        }
        lombokVersion = '1.18.12'
        springVersion = '5.2.6.RELEASE'

        springDataVersion = '2.2.7.RELEASE'
        postgresVersion = '42.2.12'
        hibernateVersion = '5.4.14.Final'
        hibernateTypesVersion = '2.9.9'

        springBootVersion = '2.2.6.RELEASE'
        jettyWebappVersion = '9.4.28.v20200408'
        swaggerVersion = '2.9.2'
        jacksonVersion = '2.11.0'

        //plugins
        pitestVersion = '1.4.6'
        flywayVersion = '6.4.1'
        repositories(repos)
    }
}


plugins {
    id 'groovy'
    id 'java'
    id 'idea'
    id 'info.solidsoft.pitest' version "$pitestVersion"
    id 'checkstyle'
    id "org.flywaydb.flyway" version "$flywayVersion"
}

apply from: 'gradle/test.gradle'

group 'com.trzewik.spring'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories(repos)

dependencies {
    compile "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"
    compile "org.eclipse.jetty:jetty-webapp:${jettyWebappVersion}"
    compile "io.springfox:springfox-swagger2:${swaggerVersion}"
    compile "io.springfox:springfox-swagger-ui:${swaggerVersion}"

    compile "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
    compile "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    compile "com.fasterxml.jackson.core:jackson-annotations:${jacksonVersion}"

    compile "org.springframework.data:spring-data-jpa:${springDataVersion}"
    compile "org.postgresql:postgresql:${postgresVersion}"
    compile "org.springframework:spring-orm:${springVersion}"
    compile "org.hibernate:hibernate-core:${hibernateVersion}"
    compile "com.vladmihalcea:hibernate-types-52:${hibernateTypesVersion}"

    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
}

checkstyle {
    toolVersion "8.1"
}

pitest {
    threads = 4
    outputFormats = ['XML', 'HTML']
    timestampedReports = false
}

flyway {
    url = 'jdbc:postgresql://127.0.0.1:54320/'
    user = 'postgres'
    password = 'example'
    schemas = ['spring_db']
    defaultSchema = 'spring_db'
}
